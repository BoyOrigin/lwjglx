apply plugin: 'java-library'
apply plugin: 'maven-publish'

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
				? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
				: "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
				? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
				: "natives-windows-x86"
		break
}

group = 'org.lwjgl'
version = '0.18'
buildDir = new File(rootProject.projectDir, "build/")

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	withSourcesJar()
}

tasks.withType(JavaCompile) {
	// If building with JDK 9+, we need additional flags to generate compatible bytecode
	if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
		options.compilerArgs.addAll(Arrays.asList("--release", "8"))
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation platform("org.lwjgl:lwjgl-bom:3.3.1")

	api "org.lwjgl:lwjgl"
	api "org.lwjgl:lwjgl-glfw"
	api "org.lwjgl:lwjgl-openal"
	api "org.lwjgl:lwjgl-opengl"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	implementation  'com.google.code.findbugs:jsr305:3.0.2'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
	useJUnitPlatform()
}

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.java
				groupId = 'com.github.Fox2Code'
				artifactId = 'lwjglx'
				version = '1.0' // JitPack only work with "1.0" as version here.
			}
		}
	}
}